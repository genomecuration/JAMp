#!/usr/bin/env perl

=pod

=head1 NAME

L<Transdecoder|http://transdecoder.sourceforge.net> - Transcriptome Protein Prediction

=head1 USAGE

Required:

 -t <string>                            transcripts.fasta

Optional:
 
 --reuse                                If this option is given, any existing files are not overwritten but reused

 --train <string>                       FASTA file with ORFs to train Markov Mod for protein identification; otherwise 
                                        longest non-redundant ORFs used

 -m <int>                               minimum protein length (default: 100)

 --workdir                              Force temporary output directory to this directory (e.g. if --reuse is needed)

 -G <string>                            genetic code (default: universal; see PerlDoc; options: Euplotes, Tetrahymena, Candida, Acetabularia)


 -h                                     print this option menu and quit
 -v                                     verbose

 -S                                     strand-specific (only analyzes top strand)
 -T <int>                               If no --train, top longest ORFs to train Markov Model (hexamer stats) (default: 500)

 --retain_long_orfs <int>               retain all ORFs found that are equal or longer than these many nucleotides even if no other evidence 
                                         marks it as coding (default: 900 bp => 300aa)

 #########################
  Pfam options:
 #########################

 --search_pfam <string>                 /path/to/pfam_db.hmm to search 
                                        using hmmscan (which should be accessible via your PATH setting)
 
 --pfam_out <string>                    You can also pre-run the pfam searches if --reuse is set. In that case, 
                                        --pfam_out is the output of hhmscan --domtblout using --noali --cut_nc --acc --notextw

 --prepare_pfam                         Prepare data for PFAM search and then quit (for running PFAM on HPC/computing cluster
                                         with or without MPI )

 --CPU <int>                            number of threads to use; (default: 2)

 --MPI                                  use MPI w/ execution of hmmscan


=head1 Genetic Codes

See L<http://golgi.harvard.edu/biolinks/gencode.html>. These are currently supported:

 universal (default)
 Euplotes
 Tetrahymena
 Candida
 Acetabularia
 Mitochondrial-Canonical
 Mitochondrial-Vertebrates
 Mitochondrial-Arthropods
 Mitochondrial-Echinoderms
 Mitochondrial-Molluscs
 Mitochondrial-Ascidians
 Mitochondrial-Nematodes
 Mitochondrial-Platyhelminths
 Mitochondrial-Yeasts
 Mitochondrial-Euascomycetes
 Mitochondrial-Protozoans

=head1 PFAM 

You will need hmmer installed. Use hmmpress to prepare the database for hmmer.
 L<See|https://sourceforge.net/projects/transdecoder/files/Pfam-AB.hmm.bin> for downloading the database.

=head1 CD-HIT

CD-HIT is available L<from|http://www.bioinformatics.org/downloads/index.php/cd-hit/cd-hit-v4.5.4-2011-03-07.tgz>

=cut



use strict;
use warnings;
use FindBin;
use Pod::Usage;
use Getopt::Long qw(:config no_ignore_case bundling pass_through);
use Data::Dumper;
use List::Util qw (min max);
use File::Basename;

use lib ("$FindBin::RealBin/PerlLib");

use POSIX qw(ceil);
use Gene_obj;
use Nuc_translator;
use Fasta_reader;
use Longest_orf;

my $UTIL_DIR = "$FindBin::RealBin/util";
$ENV{PATH} .= ":$UTIL_DIR/bin";

my ($cd_hit_est_exec) = &check_program('cd-hit-est');

my ($transcripts_file,$train_file,$prepare_pfam_only);

my $min_prot_length = 100;
my $genetic_code='universal';

my $top_ORFs_train = 500;

my $TOP_STRAND_ONLY = 0;

my $help;
my $workdir;
my $verbose;
my $search_pfam = "";
my ($reuse,$pfam_out);
my $CPU = 2;
my $RETAIN_LONG_ORFS = 900;
my $MPI = 0;


my $MPI_DEBUG = 1;

&GetOptions( 't=s' => \$transcripts_file,
             'train:s' => \$train_file,
             'm=i' => \$min_prot_length,
             'G=s' => \$genetic_code,
             'h' => \$help,
             'v' => \$verbose,
             'S' => \$TOP_STRAND_ONLY, 
             'T=i' => \$top_ORFs_train,
             'CPU=i' => \$CPU,
             'search_pfam=s' => \$search_pfam,
             'reuse' => \$reuse,
             'workdir:s' => \$workdir,
             'pfam_out:s' => \$pfam_out,
             'retain_long_orfs=i' => \$RETAIN_LONG_ORFS,
             'prepare_pfam' => \$prepare_pfam_only,
             'debug' => \$MPI_DEBUG,
             'MPI' => \$MPI,
             );



pod2usage if ($help);

if (@ARGV) {
    die "Error, don't understand options: @ARGV";
}

# this cannot work with perl $ENV{LD_LIBRARY_PATH} .= ":$UTIL_DIR/lib64/";
die "MPI requested but you have not added $UTIL_DIR\/lib64 to your LD_LIBRARY_PATH environmental variable. Please read the manual\n" if $MPI && $ENV{LD_LIBRARY_PATH}!~/$UTIL_DIR\/lib64/;


$|++;

our $SEE = $verbose;

pod2usage "No transcript file (-t)\n" unless ($transcripts_file && -s $transcripts_file);

$workdir = "transdecoder.tmp.$$" unless $workdir;
mkdir($workdir) unless -d $workdir;
die "Error, cannot mkdir $workdir" unless -d $workdir;

if ($genetic_code ne 'universal') {
    &Nuc_translator::use_specified_genetic_code($genetic_code);
}

my $number_of_peps = int(0);
my $prefix = "$workdir/longest_orfs";
my $cds_file = "$prefix.cds";
my $gff3_file = "$prefix.gff3";
my $pep_file = "$prefix.pep";
die "PFAM out file not found: $pfam_out\n" if $pfam_out && !-s $pfam_out;
$pfam_out = basename($transcripts_file) . ".transdecoder.pfam.dat" unless $pfam_out && -s $pfam_out;

my (%orf_lengths,$cmd);

if ($reuse && -s $pep_file && -s $cds_file && -s $gff3_file) {
    $number_of_peps = `grep -c "^>" $pep_file` ;
    chomp($number_of_peps);
    open (IN,$cds_file);
    while (my $ln=<IN>){
        next unless $ln=~/^>(\S+).+len:(\d+)/;
        $orf_lengths{$1}=$2 if $1 && $2;
    }
    close IN;
}
else {
	open (PEP, ">$pep_file") or die $!;
	open (CDS, ">$cds_file") or die $!; 
	open (GFF, ">$gff3_file") or die $!;
	
	
	my $counter = 0;
	
	my $fasta_reader = new Fasta_reader($transcripts_file);
	while (my $seq_obj = $fasta_reader->next()) {
		
		my $acc = $seq_obj->get_accession();
		my $sequence = $seq_obj->get_sequence();
		
		my $longest_orf_finder = new Longest_orf();
		$longest_orf_finder->allow_5prime_partials();
		$longest_orf_finder->allow_3prime_partials();
		
	    if ($TOP_STRAND_ONLY) {
			$longest_orf_finder->forward_strand_only();
		}
		
		my @orf_structs = $longest_orf_finder->capture_all_ORFs($sequence);
		
		@orf_structs = reverse sort {$a->{length}<=>$b->{length}} @orf_structs;
		
        while (@orf_structs) {
            my $orf = shift @orf_structs;
            
            my $start = $orf->{start};
            my $stop = $orf->{stop};
            
            my $length = int((abs($start-$stop)+1)/3); #int($orf->{length}/3);
            my $orient = $orf->{orient};
            my $protein = $orf->{protein};            
            
            ##################################
            # adjust for boundary conditions, since starts and stops run off the ends of the sequences at partial codons
            #################################
            
            # adjust at 3' end
            if ($stop > length($sequence)) {
                $stop -= 3;
            }
            if ($start > length($sequence)) {
                $start -= 3;
            }
            
            # adjust at 5' end
            if ($stop < 1) {
                $stop += 3;
            }
            if ($start < 1) {
                $start += 3;
            }
            
                        
            if ($length < $min_prot_length) { next; }
            
            my $cds_coords_href = { $start => $stop };
            my $exon_coords_href = ($start < $stop) ? { 1 => length($sequence) } : { length($sequence) => 1 };
            
            my $gene_obj = new Gene_obj();
            
            $counter++;
            $gene_obj->populate_gene_object($cds_coords_href, $exon_coords_href);
            $gene_obj->{asmbl_id} = $acc;
            
            my $model_id = "$acc|m.$counter";
            my $gene_id = "$acc|g.$counter";
            
            $gene_obj->{TU_feat_name} = $gene_id;
            $gene_obj->{Model_feat_name} = $model_id;

            
            my $cds = $gene_obj->create_CDS_sequence(\$sequence);
            
            unless ($cds) {
                die "Error, no CDS for gene: " . Dumper($cds_coords_href) . Dumper($exon_coords_href);
            }

            my $got_start = 0;
            my $got_stop = 0;
            if ($protein =~ /^M/) {
                $got_start = 1;
            } 
            if ($protein =~ /\*$/) {
                $got_stop = 1;
            }
            
            my $prot_type = "";
            if ($got_start && $got_stop) {
                $prot_type = "complete";
            } elsif ($got_start) {
                $prot_type = "3prime_partial";
            } elsif ($got_stop) {
                $prot_type = "5prime_partial";
            } else {
                $prot_type = "internal";
            }
            
            $gene_obj->{com_name} = "ORF $gene_id $model_id type:$prot_type len:$length ($orient)";            
            
            # this header is identical between CDS and PEP (since PEP is just a direct translation of CDS for a specific translation table)
            # we are currently not printing this out at the final data but it would be nice to.
            my $pep_header = ">$model_id $gene_id type:$prot_type len:$length gc:$genetic_code $acc:$start-$stop($orient)\n";
            my $cds_header = ">$model_id $gene_id type:$prot_type len:$length $acc:$start-$stop($orient)\n";
            
            print PEP $pep_header."$protein\n";
            $number_of_peps++;
            
            print CDS $cds_header."$cds\n";
            
            print GFF $gene_obj->to_GFF3_format(source => "transdecoder") . "\n";
            

            $orf_lengths{$model_id} = length($cds);
            


        }
	}

    close PEP;
    close CDS;
    close GFF;
    
}

## Train a Markov model based on user-provided file or longest candidate CDS sequences, score all candidates, and select the final set.

my $top_cds_file = $train_file && -s $train_file ? $train_file : "$cds_file.top_${top_ORFs_train}_longest";
if (!-s $top_cds_file) {
    # get longest entries
    my $cmd = "$UTIL_DIR/get_top_longest_fasta_entries.pl $cds_file $top_ORFs_train > $top_cds_file";
    
    unless ($reuse && -s $top_cds_file){
        if ($cd_hit_est_exec){
            # to speed things up only check for redundancy up to 4x the number of entries we want
            my $red_num = $top_ORFs_train * 4 ;
            &process_cmd("$UTIL_DIR/get_top_longest_fasta_entries.pl $cds_file $red_num > $workdir/redundant_top");
            &process_cmd("$cd_hit_est_exec -r 1 -i $workdir/redundant_top -o $workdir/redundant_top.nr90 -M 0 -T $CPU >/dev/null 2>/dev/null");
            &process_cmd("$UTIL_DIR/get_top_longest_fasta_entries.pl $workdir/redundant_top.nr90 $top_ORFs_train > $top_cds_file");
            unlink("$workdir/redundant_top");
            unlink("$workdir/redundant_top.nr90");
            unlink("$workdir/redundant_top.nr90.bak.clstr");
        }
        else {
            &process_cmd($cmd);
        }
    }
}

$cmd = "$UTIL_DIR/compute_base_probs.pl $transcripts_file $TOP_STRAND_ONLY > $workdir/base_freqs.dat";
&process_cmd($cmd) unless $reuse && -s "$workdir/base_freqs.dat";


# get hexamer scores
#$cmd = "$UTIL_DIR/seq_n_background_to_logliklihood_vals.pl $top_cds_file $transcripts_file.random > hexamer.scores";
#&process_cmd($cmd) unless ($reuse && -s "hexamer.scores");

$cmd = "$UTIL_DIR/seq_n_baseprobs_to_logliklihood_vals.pl $top_cds_file $workdir/base_freqs.dat > $workdir/hexamer.scores";
&process_cmd($cmd) unless $reuse && -s "$workdir/hexamer.scores";


# score all cds entries
$cmd = "$UTIL_DIR/score_CDS_liklihood_all_6_frames.pl $cds_file $workdir/hexamer.scores > $cds_file.scores";
&process_cmd($cmd) unless ($reuse && -s "$cds_file.scores");


#############
# run pfam
#############

my %has_pfam_hit;
if ($search_pfam) {

    my $cmd = "$FindBin::Bin/pfam_runner.pl --pep $pep_file --pfam_db $search_pfam --CPU $CPU -o $pfam_out ";

    if ($MPI) {
        $cmd .= " --MPI ";
    }
    
    if ($reuse) {
        $cmd .= " --reuse ";
    }
    if ($workdir) {
        $cmd .= " --workdir $workdir ";
    }
    if ($prepare_pfam_only) {
        $cmd .= " --prepare_pfam ";
    }
    
    if ($MPI_DEBUG) {
        $cmd .= " --debug ";
    }
    
    &process_cmd($cmd);
    exit if $prepare_pfam_only;
}
if (-s $pfam_out){
    print "PFAM output found and processing...\n";
    # capture those proteins having pfam hits
    open (my $fh, $pfam_out) or die "Error, cannot open file: $pfam_out";
    while (my $ln=<$fh>) {
        next if $ln=~/^\#/;
        my @x = split(/\s+/,$ln);
        next unless $x[3];  # domtbl
        my $orf_acc = $x[3];
        $has_pfam_hit{$orf_acc} = 1;
    }
    close $fh;
}

# get accs for best entries
my $acc_file = "$cds_file.scores.selected";
{
	open (my $ofh, ">$acc_file") or die "Error, cannot write to $acc_file";
	open (my $ifh, "$cds_file.scores") or die "Error, cannot open file $cds_file.scores";
	while (<$ifh>) {
		chomp;
		my ($acc, @scores) = split(/\t/);
		
		my $score_1 = shift @scores;
		my $max_score_other_frame = max(@scores);
		if ($has_pfam_hit{$acc} 
            || 
            $orf_lengths{$acc} >= $RETAIN_LONG_ORFS
            ||
            ($score_1 > 0 && $score_1 > $max_score_other_frame)
            ) { 
			print $ofh "$acc\n";
            
            if ($has_pfam_hit{$acc}) {
                print STDERR "-$acc flagged as having a pfam domain.\n" if $verbose;
            }
            
        }
	}
	close $ifh;
	close $ofh;
}

# index the current gff file:
$cmd = "$UTIL_DIR/index_gff3_files_by_isoform.pl $gff3_file";
&process_cmd($cmd);

# retrieve the best entries:
$cmd = "$UTIL_DIR/gene_list_to_gff.pl $acc_file $gff3_file.inx > $cds_file.best_candidates.gff3";
&process_cmd($cmd);

{
    my $final_output_prefix = basename($transcripts_file) . ".transdecoder";
    
    # exclude shadow orfs (smaller orfs in different reading frame that are eclipsed by longer orfs)
    $cmd = "$UTIL_DIR/remove_eclipsed_ORFs.pl $cds_file.best_candidates.gff3 > $final_output_prefix.gff3";
    &process_cmd($cmd);
    


    ## write final outputs:
    
    ## make a BED file for viewing in IGV
    my $gff3_file = "$final_output_prefix.gff3";
    my $bed_file = $gff3_file;
    $bed_file =~ s/\.gff3$/\.bed/;
    $cmd = "$UTIL_DIR/gff3_file_to_bed.pl $gff3_file > $bed_file";
    &process_cmd($cmd);
    
    
    # make a peptide file:
    my $best_pep_file = $gff3_file;
    $best_pep_file =~ s/\.gff3$/\.pep/;
    $cmd = "$UTIL_DIR/gff3_file_to_proteins.pl $gff3_file $transcripts_file > $best_pep_file";
    &process_cmd($cmd);



    # make a CDS file:
    my $best_cds_file = $best_pep_file;
    $best_cds_file =~ s/\.pep$/\.cds/;
    $cmd = "$UTIL_DIR/gff3_file_to_proteins.pl $gff3_file $transcripts_file CDS > $best_cds_file";
    &process_cmd($cmd);
    
}

print STDERR "transdecoder is finished.\n";


exit(0);


####
sub process_cmd {
	my ($cmd) = @_;

	print "CMD: $cmd\n";
	my $ret = system($cmd);

	if ($ret) {
		die "Error, cmd: $cmd died with ret $ret";
	}
	
	return;

}


sub check_program() {
 my @paths;
 foreach my $prog (@_) {
  my $path = `which $prog`;
  die "Error, path to a required program ($prog) cannot be found\n\n"
    unless $path =~ /^\//;
  chomp($path);
  $path = readlink($path) if -l $path;
  push( @paths, $path );
 }
 return @paths;
}
